<?xml version="1.0"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="init()">
    <mx:Script><![CDATA[
        import event.IconEvent;

        private var processIconList:Array = [];
        private var currentSelected:ProcessIcon = null;

        private function init():void {
            this.graphics.clear();
            //填充背景
            this.graphics.beginFill(0xFFFFFF);
            this.graphics.drawRect(0, 0, this.width, this.height);
            this.graphics.endFill();
        }

        public function draw(processDefinitionXml:XML):void {
            // 清空画布
            this.graphics.clear();

            var model:Namespace = processDefinitionXml.namespace();

            var bpmndi:Namespace = getNameSpace(processDefinitionXml, "http://www.omg.org/spec/BPMN/20100524/DI");

            var processNodeList:XMLList = processDefinitionXml.model::process.*;
            var node:XML;
            var processIcon:ProcessIcon;

            for (var i:int = 0; i < processNodeList.length(); i++) {
                node = processNodeList[i];

                var diNode:XML;
                if(bpmndi != null) {
                    trace(node.@id.toString());

                    var list:XMLList = processDefinitionXml.bpmndi::BPMNDiagram..bpmndi::BPMNShape.(@bpmnElement == node.@id.toString());
                    if(list.length() > 0) {
                        diNode = list[0];
                    } else {
                        list = processDefinitionXml.bpmndi::BPMNDiagram..bpmndi::BPMNEdge.(@bpmnElement == node.@id.toString());
                        if(list.length() > 0) {
                            diNode = list[0];
                        }
                    }
                }

                processIcon = new ProcessIcon(node,  diNode);
                processIcon.addEventListener(IconEvent.ICON_MOUSE_DOWN, onIconMouseDownHandler);
                processIconList.push(processIcon);
                this.addChild(processIcon);
            }
        }

        internal function onIconMouseDownHandler(event:IconEvent):void {
//            clearAllSelected();
            trace(event.type);
            /*currentSelected = event.icon;
            currentSelected.selected();*/
        }

        /**
         * 获取指定uri的命名空间
         *
         * @param xml
         * @param uriStr
         * @return
         */
        private static function getNameSpace(xml:XML, uriStr:String):Namespace {
            for(var i:uint = 0; i < xml.namespaceDeclarations().length; i++) {
                var ns:Namespace = xml.namespaceDeclarations()[i];
                var uri:String = ns.uri;
                if(uri == uriStr) {
                    return ns;
                }
            }

            return null;
        }

        public function highlightNode(key:String):void {
            var processIcon:ProcessIcon;
            for(var i:int = 0; i < processIconList.length; i++) {
                processIcon = processIconList[i];
                if(processIcon.key == key) {
                    processIcon.highlight();
                }
            }
        }

        // 清除所有选中
        private function clearAllSelected():void {
            for(var i:int = 0; i < processIconList.length; i++) {
                var processIcon:ProcessIcon = processIconList[i];
                processIcon.clearSelected();
            }
        }

        // 清除所有高亮
        private function clearAllHighlight():void {
            for(var i:int = 0; i < processIconList.length; i++) {
                var processIcon:ProcessIcon = processIconList[i];
                processIcon.clearHighlight();
            }
        }

        // 获取当前选中的节点
        public function getCurrentSelected():ProcessIcon {
            return currentSelected;
        }
        ]]></mx:Script>
</mx:Canvas>
